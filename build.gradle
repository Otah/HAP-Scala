
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.32"
    }
}

ext.scalaMajor = '2.13'
ext.scalaMinor = 2
ext.scalaLib = ext.scalaMajor + '.' + ext.scalaMinor

def scala(libName) {
    return libName + '_' + ext.scalaMajor
}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.maiflai.scalatest'

    ext {
        monix = [ group: 'io.monix', name: scala('monix'), version: '3.1.0'] //TODO add some systematic versioning
        rxScala = [ group: 'io.reactivex', name: scala('rxscala'), version: '0.27.0']
        beowulfeHap = [ group: 'io.github.hap-java', name: 'hap', version: '2.0.0' ]
        scaffeine = [ group: 'com.github.blemale', name: scala('scaffeine'), version: '3.1.0' ]
        scalajson = [ group: 'io.spray', name: scala('spray-json'), version: '1.3.6' ]
        slf4jSimple = [ group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25' ]
    }

    group = "com.github.otah"
    version = "0.13.0-server-SNAPSHOT"

    sourceCompatibility = 11

    dependencies {
        implementation group: 'org.scala-lang', name: 'scala-library', version: scalaLib
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        implementation group: 'com.typesafe.scala-logging', name: scala('scala-logging'), version: '3.9.4'

        testImplementation group: 'org.scalatest', name: scala('scalatest'), version: '3.2.10'
        testRuntimeOnly group: 'com.vladsch.flexmark', name: 'flexmark-all', version: '0.62.2'
    }

    configurations.all {
        exclude group: "log4j", module: "log4j"
    }

    compileScala {
        scalaCompileOptions.additionalParameters = ["-feature"]
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId scala(project.name)

                from components.java
            }
        }
    }
}
