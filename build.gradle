
allprojects {
    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
        }
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = 'https://dl.bintray.com/openhab/mvn/'
        }
    }
}

ext.scalaMajor = '2.13'
ext.scalaMinor = 2
ext.scalaLib = ext.scalaMajor + '.' + ext.scalaMinor

def scala(libName) {
    return libName + '_' + ext.scalaMajor
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'maven-publish'

    ext {
        monix = [ group: 'io.monix', name: scala('monix'), version: '3.1.0'] //TODO add some systematic versioning
        rxScala = [ group: 'io.reactivex', name: scala('rxscala'), version: '0.27.0']
        beowulfeHap = [ group: 'io.github.hap-java', name: 'hap', version: '2.0.0.OH' ]
        scaffeine = [ group: 'com.github.blemale', name: scala('scaffeine'), version: '3.1.0' ]
        scalajson = [ group: 'io.spray', name: scala('spray-json'), version: '1.3.6' ]
    }

    group = "com.github.otah"
    version = "0.9.0-iids-SNAPSHOT"

    sourceCompatibility = 1.8

    dependencies {
        implementation group: 'org.scala-lang', name: 'scala-library', version: scalaLib
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        implementation group: 'com.typesafe.scala-logging', name: scala('scala-logging'), version: '3.9.2'

        testImplementation group: 'junit', name: 'junit', version: '4.11'
        testImplementation group: 'org.scalatest', name: scala('scalatest'), version: '3.1.2'
        testImplementation group: 'org.scalatestplus', name: scala('scalatestplus-junit'), version: '1.0.0-M2'
    }

    configurations.all {
        exclude group: "log4j", module: "log4j"
    }

    compileScala {
        scalaCompileOptions.additionalParameters = ["-feature"]
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId scala(project.name)

                from components.java
            }
        }
    }
}
